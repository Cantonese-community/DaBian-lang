使下 |python-re|

咩系 Lexer?
  佢个老豆叫 object
  佢识得 |__init__| |代码, 关键字| => {
    讲嘢 -> {
      |自己嘅 code| 係 代码
      |自己嘅 keywords| 係 关键字
      |自己嘅 line| 係 1
    }
    @~self.re_number = r"^0[xX][0-9a-fA-F]*(\.[0-9a-fA-F]*)?([pP][+\-]?[0-9]+)?|^[0-9]*(\.[0-9]*)?([eE][+\-]?[0-9]+)?"#
    @~self.re_new_line = re.compile(r"\r\n|\n\r|\n|\r")#
    @~self.re_id = r"^[_\d\w]+|^[\u4e00-\u9fa5]+"#
    @~self.re_str = r"(?s)(^'(\\\\|\\'|\\\n|\\z\s*|[^'\n])*')|(^\"(\\\\|\\\"|\\\n|\\z\s*|[^\"\n])*\")"#
    @~self.re_expr = r"[(](.*?)[)]"#
  }

  /* Skip n chars */
  佢识得 |跳过| |n| => {
    讲嘢: |自己嘅 code| 係 |自己嘅 code[n:]|
  }

  佢识得 |识别空格| |c| => {
    还数 |c 喺 ('\t', '\n', '\v', '\f', '\r', ' ')|
  }

  佢识得 |识别换行| |c| => {
    还数 |c 喺 ('\r', '\n')|
  }

  佢识得 |识别中文字| |word| => {
    i 从 0 行到 |word 嘅长度| 
      如果 | '\u4e00' <= word[i] <= '\u9fff' | 嘅话 -> {
        还数 啱
      }
    行晒
    还数 唔啱
  }

  /* Found the token by pattern */
  佢识得 |扫描| |模式| => {
    讲嘢: m 係 |re.match(模式, 自己嘅 code)|
    如果 m 嘅话 -> {
      讲嘢: Token 係 |m.group()|
      |自己嘅 跳过| 下 -> |Token 嘅长度|
      还数 Token
    }
  }

  佢识得 |扫描标识符| => {
    还数 |自己嘅 扫描(自己嘅 re_id)|
  }

  佢识得 |扫描数字| => {
    还数 |自己嘅 扫描(自己嘅 re_number)|
  }

  佢识得 |扫描字符串| => {
    还数 |自己嘅 扫描(自己嘅 re_str)|
  }

  佢识得 |扫描表达式| => {
    还数 |自己嘅 扫描(自己嘅 re_expr)|
  }

  佢识得 |检查| |s| => {
    还数 |自己嘅 code.startswith(s)|
  }

  佢识得 |跳过空格换行| => {
    落操场玩跑步
      如果 |自己嘅 检查('\r\n') 或者 自己嘅 检查('\n\r')| 嘅话 -> {
        |自己嘅 跳过| 下 -> 2
        讲嘢: |自己嘅 line| 係 |自己嘅 line + 1|
      }
      定系 |自己嘅 识别换行(自己嘅 code[0])| 嘅话 -> {
        |自己嘅 跳过| 下 -> 1
        讲嘢: |自己嘅 line| 係 |自己嘅 line + 1|
      }
      定系 |自己嘅 检查('?') 或者 自己嘅 检查(':') 或者 自己嘅 检查('：') 或者 \
            自己嘅 检查('？') 或者 自己嘅 检查('!') 或者 自己嘅 检查('！') 或者 \
            自己嘅 检查(',')| 嘅话 -> {
        |自己嘅 跳过| 下 -> 1
      }
      定系 |自己嘅 识别空格(自己嘅 code[0])| 嘅话 -> {
        |自己嘅 跳过| 下 -> 1
      }
      唔系 嘅话 -> {
        饮茶先啦
      }
    玩到 |自己嘅 code 嘅长度 <= 0| 为止
  }

  佢识得 获取token => {
    |自己嘅 跳过空格换行| 下 -> ||
    如果 |自己嘅 code 嘅长度 == 0| 嘅话 -> {
      还数 |[自己嘅 line, ['EOF', 'EOF']]|
    }

    讲嘢: c 係 |自己嘅 code[0]|

    如果 |c == '-'| 嘅话 -> {
      如果 |自己嘅 检查('->')| 嘅话 -> {
        |自己嘅 跳过| 下 -> 2
        还数 |[自己嘅 line, ['keyword', '->']]|
      }
    }

    如果 |c == '{'| 嘅话 -> {
      |自己嘅 跳过| 下 -> 1
      还数 |[自己嘅 line, ['keyword', '{']]|
    }

    如果 |c == '}'| 嘅话 -> {
      |自己嘅 跳过| 下 -> 1
      还数 |[自己嘅 line, ['keyword', '}']]|
    }

    如果 |c 喺 ('\'', "\"")| 嘅话 -> {
      还数 |[自己嘅 line, ['string', 自己嘅 扫描字符串()]]|
    }

    如果 |c == '('| 嘅话 -> {
      还数 |[自己嘅 line, ['expr', 自己嘅 扫描表达式()]]|
    }

    如果 |自己嘅 识别中文字(c) 或者 c 系 '_' 或者 c.isalpha()| 嘅话 -> {
      讲嘢: token 係 |自己嘅 扫描标识符()|
      如果 |token 喺 自己嘅 keywords| 嘅话 -> {
        还数 |[自己嘅 line, ['keyword', token]]|
      }
      还数 |[自己嘅 line, ['identifier', token]]|
    }

    如果 |c 系 '.' 或者 c.isdigit()| 嘅话 -> {
      讲嘢: token 係 |自己嘅 扫描数字()|
      还数 |[自己嘅 line, ['num', token]]|
    }
  }

明白未啊?